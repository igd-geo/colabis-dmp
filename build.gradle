plugins {
  id 'com.moowork.node' version '1.2.0'
  id 'distribution'
}

subprojects {
  apply plugin: 'com.moowork.node'

  node {
    // Version of node to use.
    version = '8.12.0'

    // Version of npm to use.
    npmVersion = '6.4.1'

    // Version of Yarn to use.
    yarnVersion = '1.12.3'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.projectDir}/.gradle/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.projectDir}/.gradle/npm")

    // Set the work directory for Yarn
    yarnWorkDir = file("${project.projectDir}/.gradle/yarn")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}")
  }

  distributions {
    main {
      contents {
        // include 'conf' and 'keys' directories in distribution
        from(projectDir) {
          include 'conf/**/*'
          include 'keys/**/*'
        }
      }
    }
  }
}

installDist.dependsOn "server:installDist"

distZip.dependsOn installDist
distTar.dependsOn installDist

distributions {
  main {
    baseName = project.name
    contents {
      project.subprojects.each { sub ->
        into('') {
          from "${sub.buildDir}/install/${sub.name}"
          include 'bin/**/*'
          include 'lib/**/*'
          include 'conf/**/*'
        }
      }
    }
  }
}


wrapper {
  gradleVersion = '4.10'
}